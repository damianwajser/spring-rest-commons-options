group 'dwajser'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.jfrog.bintray'
version = currentVersion

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3"
    }
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile group: 'org.springframework', name: 'spring-web', version: '4.3.12.RELEASE'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.2'
}
jar {
  manifest {
    attributes(
      "Class-Path": configurations.compile.collect { it.getName() }.join(' '))
  }
}
test {
    if (System.getProperty('testsDebug', 'true').toBoolean()) {
        testLogging {
            exceptionFormat "full"
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            minGranularity 0
        }
    }
    systemProperties = [
        'http.proxyHost': System.getProperty('http.proxyHost'),
        'http.proxyPort': System.getProperty('http.proxyPort'),
        'http.proxyUser': System.getProperty('http.proxyUser'),
        'http.proxyPassword': System.getProperty('http.proxyPassword')
    ]
}


def javaApiUrl = 'http://docs.oracle.com/javase/1.7.0/docs/api/'
tasks.withType(Javadoc) {
    options.links(javaApiUrl)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

//jar.dependsOn sourcesJar

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "damianwajser"
            name "Damian Wajser"
            email "wajser@gmail.com"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A gradle plugin for publishing to Bintray')
                root.children().last() + pomConfig
            }
        }
    }
}

task createReleasePropertiesFile(type:Exec) {
    String fileName = 'bintray.plugin.release.properties'
    println 'Creating $fileName'
    String fileContent = "version=$currentVersion"
   // (new File("$rootDir/src/main/resources/$fileName")).write(fileContent)
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    resolve {
        repository {
            repoKey = 'libs-release'
        }
    }
    publish {
        repository {
            repoKey = 'oss-snapshot-local' //The Artifactory repository key to publish to
            //when using oss.jfrog.org the credentials are from Bintray. For local build we expect them to be found in
            //~/.gradle/gradle.properties, otherwise to be set in the build server
            username = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
            password = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
        }
        defaults {
            publications 'mavenJava'
            properties = ['bintray.repo': 'jfrog/jfrog-jars', 'bintray.package': 'gradle-bintray-plugin', 'bintray.version': version.toString()]
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        userOrg = 'damianwajser'
        name = 'spring-rest-commons-options'
        licenses = ['Apache-2.0']
        version {
            attributes = ['gradle-plugin': 'com.jfrog.bintray:com.jfrog.bintray.gradle:gradle-bintray-plugin']
        }
    }
}

task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal') {
    doLast {
        logger.info "Installing $project.name"
    }
}

task wrapper(type: Wrapper, description: 'Gradle Wrapper task') {
    gradleVersion = '2.4'
}